# Maintainer: Roberto Catini <roberto.catini@gmail.com>

pkgname=cbcd
pkgrel=1
pkgver=0
pkgdesc="cbc peer-to-peer network daemon"
arch=('i686' 'x86_64')
url="https://github.com/cbc/cbcd"
license=('custom:ISC')
depends=('protobuf' 'openssl' 'boost-libs')
makedepends=('git' 'scons' 'boost')
backup=("etc/$pkgname/cbcd.cfg")
source=("git://github.com/cbc/cbcd.git#branch=master")
sha512sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	cd "$srcdir/$pkgname"
	scons
}

check() {
	cd "$srcdir/$pkgname"
	build/cbcd --unittest
}

package() {
	cd "$srcdir/$pkgname"
	install -D -m644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -D build/cbcd "$pkgdir/usr/bin/cbcd"
	install -D -m644 doc/cbcd-example.cfg "$pkgdir/etc/$pkgname/cbcd.cfg"
	mkdir -p "$pkgdir/var/lib/$pkgname/db"
	mkdir -p "$pkgdir/var/log/$pkgname"
}
